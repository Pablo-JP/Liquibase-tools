CREATE OR REPLACE  PROCEDURE SC_CREDIT.SP_SYNC_LOAN_CONCEPT_TYPE 
 (
    PA_SYNC_JSON        CLOB,
    PA_UPDATED_ROWS OUT NUMBER,
    PA_STATUS_CODE  OUT NUMBER,
    PA_STATUS_MSG   OUT VARCHAR2
  ) IS
/* **************************************************************
* PROJECT: NCP
* DESCRIPTION: CATALOG SYNCHONIZATION TC_LOAN_CONCEPT_TYPE
* CREATED DATE: 2025/01/09
* CREATOR: CESAR CORTES
* MODIFICATION DATE: 2025/01/09
************************************************************** */
    CSL_0 CONSTANT SIMPLE_INTEGER := 0;
BEGIN
  PA_STATUS_CODE := CSL_0;
  PA_STATUS_MSG := 'OK';
  PA_UPDATED_ROWS  := CSL_0;

     MERGE INTO SC_CREDIT.TC_LOAN_CONCEPT_TYPE A
  USING (
    SELECT
      *
    FROM
      JSON_TABLE ( PA_SYNC_JSON, '$.loanConceptType[*]'
        COLUMNS (
          ID NUMBER PATH '$.id',
          description VARCHAR2 ( 50 ) PATH '$.description',
          STATUS NUMBER PATH '$.status',
          USER_NAME VARCHAR2 ( 50 ) PATH '$.user',
          CREATED_DATE TIMESTAMP PATH '$.createdDate',
          MODIFICATION_DATE TIMESTAMP PATH '$.modificationDate'
        )
      )
  ) B ON ( A.FI_LOAN_CONCEPT_TYPE_ID = B.ID )
  WHEN MATCHED THEN UPDATE
  SET A.FC_LOAN_CONCEPT_TYPE_DESC = B.description,
	    A.FI_STATUS=B.STATUS,
      A.FC_USER = B.USER_NAME,
      A.FD_CREATED_DATE = B.CREATED_DATE,
      A.FD_MODIFICATION_DATE = CAST(B.MODIFICATION_DATE AS DATE)
  WHEN NOT MATCHED THEN
  INSERT (
    FI_LOAN_CONCEPT_TYPE_ID,
    FC_LOAN_CONCEPT_TYPE_DESC,
    FI_STATUS,
    FC_USER,
    FD_CREATED_DATE,
    FD_MODIFICATION_DATE )
  VALUES
    ( B.ID,
      B.description,
      B.STATUS,
      B.USER_NAME,
      B.CREATED_DATE,
      B.MODIFICATION_DATE );

      PA_UPDATED_ROWS := SQL%ROWCOUNT;

  COMMIT;

  EXCEPTION
    WHEN OTHERS THEN
      ROLLBACK;
      PA_STATUS_CODE := SQLCODE;
      PA_STATUS_MSG  := SQLERRM;
      SC_CREDIT.SP_ERROR_LOG('SP_SYNC_LOAN_CONCEPT_TYPE', SQLCODE, SQLERRM, DBMS_UTILITY.FORMAT_ERROR_BACKTRACE, NULL,'');

END SP_SYNC_LOAN_CONCEPT_TYPE;

/

GRANT EXECUTE ON SC_CREDIT.SP_SYNC_LOAN_CONCEPT_TYPE TO USRPURPOSEWS
/
GRANT EXECUTE ON SC_CREDIT.SP_SYNC_LOAN_CONCEPT_TYPE TO USRNCPCREDIT1
/
