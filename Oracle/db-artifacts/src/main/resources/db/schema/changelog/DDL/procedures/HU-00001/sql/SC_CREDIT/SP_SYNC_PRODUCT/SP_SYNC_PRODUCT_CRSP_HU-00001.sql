CREATE OR REPLACE  PROCEDURE SC_CREDIT.SP_SYNC_PRODUCT (
  PA_SYNC_JSON    IN  CLOB,
  PA_UPDATED_ROWS OUT NUMBER,
  PA_STATUS_CODE  OUT NUMBER,
  PA_STATUS_MSG   OUT VARCHAR2
) IS
/* **************************************************************
* PROJECT: NCP
* DESCRIPTION: CATALOG SYNCHONIZATION TC_PRODUCT
* CREATED DATE: 2025/01/09
* CREATOR: CESAR CORTES
* MODIFICATION DATE: 2025/01/09
************************************************************** */
BEGIN
  PA_STATUS_CODE := 0;
  PA_STATUS_MSG := 'OK';
  MERGE INTO SC_CREDIT.TC_PRODUCT A
  USING (
    SELECT
      *
    FROM
      JSON_TABLE ( PA_SYNC_JSON, '$.product[*]'
        COLUMNS (
          FI_PRODUCT_ID NUMBER ( 10 ) PATH '$.id',
          FI_COUNTRY_ID NUMBER ( 3 ) PATH '$.country',
          FI_COMPANY_ID NUMBER ( 3 ) PATH '$.company',
          FC_PRODUCT_NAME VARCHAR2 ( 50 ) PATH '$.name',
          FC_PRODUCT_DESC VARCHAR2 ( 150 ) PATH '$.description',
          FD_RELEASE_DATE TIMESTAMP PATH '$.releaseDate',
          FC_PRODUCT_ADAPTER_ID VARCHAR2 ( 32 ) PATH '$.adapterId',
          FI_STATUS NUMBER ( 2 ) PATH '$.status',
          FC_USER VARCHAR2 ( 30 ) PATH '$.user',
          FD_CREATED_DATE TIMESTAMP PATH '$.createdDate',
          FD_MODIFICATION_DATE TIMESTAMP PATH '$.modificationDate'
        )
      )
  ) B ON ( A.FI_PRODUCT_ID = B.FI_PRODUCT_ID )
  WHEN MATCHED THEN UPDATE
  SET A.FI_COUNTRY_ID = B.FI_COUNTRY_ID,
      A.FI_COMPANY_ID = B.FI_COMPANY_ID,
      A.FC_PRODUCT_NAME = B.FC_PRODUCT_NAME,
      A.FC_PRODUCT_DESC = B.FC_PRODUCT_DESC,
      A.FD_RELEASE_DATE = CAST(B.FD_RELEASE_DATE AS DATE),
      A.FC_PRODUCT_ADAPTER_ID = B.FC_PRODUCT_ADAPTER_ID,
      A.FI_STATUS = B.FI_STATUS,
      A.FC_USER = B.FC_USER,
      A.FD_MODIFICATION_DATE = CAST(B.FD_MODIFICATION_DATE AS DATE)
  WHEN NOT MATCHED THEN
  INSERT (
    FI_PRODUCT_ID,
    FI_COUNTRY_ID,
    FI_COMPANY_ID,
    FC_PRODUCT_NAME,
    FC_PRODUCT_DESC,
    FD_RELEASE_DATE,
    FC_PRODUCT_ADAPTER_ID,
    FI_STATUS,
    FC_USER,
    FD_CREATED_DATE,
    FD_MODIFICATION_DATE )
  VALUES
    ( B.FI_PRODUCT_ID,
      B.FI_COUNTRY_ID,
      B.FI_COMPANY_ID,
      B.FC_PRODUCT_NAME,
      B.FC_PRODUCT_DESC,
    CAST(B.FD_RELEASE_DATE AS DATE),
      B.FC_PRODUCT_ADAPTER_ID,
      B.FI_STATUS,
      B.FC_USER,
    CAST(B.FD_CREATED_DATE AS DATE),
    CAST(B.FD_MODIFICATION_DATE AS DATE) );

  PA_UPDATED_ROWS := SQL%ROWCOUNT;
  COMMIT;
EXCEPTION
  WHEN OTHERS THEN
    ROLLBACK;
    PA_STATUS_CODE := SQLCODE;
    PA_STATUS_MSG := SQLERRM;
    SC_CREDIT.SP_ERROR_LOG('SP_SYNC_PRODUCT', SQLCODE, SQLERRM, DBMS_UTILITY.FORMAT_ERROR_BACKTRACE, NULL,'');

END SP_SYNC_PRODUCT;

/

GRANT EXECUTE ON SC_CREDIT.SP_SYNC_PRODUCT TO USRPURPOSEWS
/
GRANT EXECUTE ON SC_CREDIT.SP_SYNC_PRODUCT TO USRNCPCREDIT1
/
