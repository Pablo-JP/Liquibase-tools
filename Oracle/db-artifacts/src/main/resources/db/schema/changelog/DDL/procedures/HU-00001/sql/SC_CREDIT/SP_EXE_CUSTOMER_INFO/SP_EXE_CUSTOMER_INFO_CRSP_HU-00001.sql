CREATE OR REPLACE  PROCEDURE SC_CREDIT.SP_EXE_CUSTOMER_INFO (
    PA_CUSTOMER_ID IN SC_CREDIT.TA_LOAN.FC_CUSTOMER_ID%TYPE,
    PA_LOAN_STATUS_ID IN SC_CREDIT.TA_LOAN.FI_LOAN_STATUS_ID%TYPE DEFAULT NULL,
    PA_CUR_MAIN_INFO OUT SYS_REFCURSOR,
    PA_STATUS_CODE OUT NUMBER,
    PA_STATUS_MSG OUT VARCHAR2)

  AS
  -- GLOBAL CONSTANTS
  CSG_0                     CONSTANT SIMPLE_INTEGER := 0;
  CSG_1                     CONSTANT SIMPLE_INTEGER := 1;
  CSG_ARROW                 CONSTANT VARCHAR2(5) := ' -> ';
  CSG_X                     CONSTANT VARCHAR2(5) := 'X';
  CSG_SUCCESS_CODE          CONSTANT SIMPLE_INTEGER := 0;
  CSG_SUCCESS_MSG           CONSTANT VARCHAR2(10) := 'SUCCESS';
  CSG_NO_DATA_FOUND_CODE    CONSTANT SIMPLE_INTEGER := -20204;
  CSG_NO_DATA_FOUND_MSG     CONSTANT VARCHAR2(50) := 'THE DATA DOES NOT EXIST';
  CSG_SP_EXE_CUSTOMER_INFO  CONSTANT VARCHAR2(50) := 'SP_EXE_CUSTOMER_INFO';
/*************************************************************
  PROJECT    :  NCP-OUTSTANDING BALANCE
  DESCRIPTION:  STORED PROCEDURE TO SELECT THE CUSTOMER INFO
  CREATOR:      JOS??? DE JES???S BRAVO AGUILAR.
  CREATED DATE: AGO-29-2024
  MODIFICATION DATE: OCT-24-2024
*************************************************************/

    EXC_NO_DATA_FOUND EXCEPTION;
    PRAGMA EXCEPTION_INIT(EXC_NO_DATA_FOUND, CSG_NO_DATA_FOUND_CODE);

    VL_CUSTOMER_ID SC_CREDIT.TA_LOAN.FC_CUSTOMER_ID%TYPE;

  BEGIN
    PA_STATUS_CODE := CSG_SUCCESS_CODE;
    PA_STATUS_MSG := CSG_SUCCESS_MSG;

  SELECT COUNT(CSG_1)
  INTO VL_CUSTOMER_ID
  FROM SC_CREDIT.TA_LOAN
  WHERE FC_CUSTOMER_ID = PA_CUSTOMER_ID;
  IF VL_CUSTOMER_ID = CSG_0 THEN
    RAISE EXC_NO_DATA_FOUND;
  END IF;

  OPEN PA_CUR_MAIN_INFO FOR
  SELECT DISTINCT
    L.FI_LOAN_ID
    ,L.FI_COUNTRY_ID
    ,L.FI_COMPANY_ID
    ,L.FI_BUSINESS_UNIT_ID
    ,L.FI_ADMIN_CENTER_ID
    ,L.FI_ORIGINATION_CENTER_ID
    ,L.FC_PLATFORM_ID
    ,L.FC_SUB_PLATFORM_ID
    ,L.FC_CUSTOMER_ID
    ,L.FI_PRODUCT_ID
    ,L.FN_PRINCIPAL_AMOUNT
    ,L.FN_FINANCE_CHARGE_AMOUNT
    ,L.FN_PRINCIPAL_BALANCE
    ,L.FN_FINANCE_CHARGE_BALANCE
    ,L.FN_ADDITIONAL_CHARGE_BALANCE
    ,L.FD_ORIGINATION_DATE
    ,L.FD_FIRST_PAYMENT
    ,L.FD_DUE_DATE
    ,L.FN_APR
    ,L.FI_ADDITIONAL_STATUS
    ,L.FI_CURRENT_BALANCE_SEQ
    ,L.FN_INTEREST_RATE
    ,L.FI_NUMBER_OF_PAYMENTS
    ,L.FI_TERM_TYPE
    ,L.FI_LOAN_STATUS_ID
    ,L.FC_SUBPRODUCT_ID
    ,L.FI_ACCRUED_TYPE_ID
    ,L.FI_RULE_ID
    ,L.FC_END_USER
    ,L.FI_TRANSACTION
    ,L.FD_OPERATION_DATE
    ,L.FC_UUID_TRACKING
    ,L.FC_IP_ADDRESS
    ,L.FC_DEVICE
    ,LB.FI_LOAN_BALANCE_ID
    ,LB.FI_LOAN_OPERATION_ID
    ,LB.FI_BALANCE_SEQ
    ,LB.FN_PRINCIPAL_BALANCE
    ,LB.FN_FINANCE_CHARGE_BALANCE
    ,LB.FN_ADDITIONAL_CHARGE_BALANCE
  FROM
    SC_CREDIT.TA_LOAN L
  LEFT OUTER JOIN SC_CREDIT.TA_LOAN_BALANCE LB
  ON L.FI_LOAN_ID = LB.FI_LOAN_ID
  AND L.FI_CURRENT_BALANCE_SEQ = LB.FI_BALANCE_SEQ
  WHERE
    L.FC_CUSTOMER_ID = PA_CUSTOMER_ID
    AND (PA_LOAN_STATUS_ID IS NULL OR L.FI_LOAN_STATUS_ID = PA_LOAN_STATUS_ID);

  EXCEPTION
  WHEN EXC_NO_DATA_FOUND THEN
  PA_STATUS_CODE := CSG_NO_DATA_FOUND_CODE;
  PA_STATUS_MSG := CSG_NO_DATA_FOUND_MSG || '->' || DBMS_UTILITY.FORMAT_ERROR_BACKTRACE;
  OPEN PA_CUR_MAIN_INFO FOR
  SELECT
    NULL AS FI_LOAN_ID
    ,NULL AS FI_COUNTRY_ID
    ,NULL AS FI_COMPANY_ID
    ,NULL AS FI_BUSINESS_UNIT_ID
    ,NULL AS FC_ADMIN_CENTER_ID
    ,NULL AS FC_ORIGINATION_CENTER_ID
    ,NULL AS FC_PLATFORM_ID
    ,NULL AS FC_SUB_PLATFORM_ID
    ,NULL AS FC_CUSTOMER_ID
    ,NULL AS FI_PRODUCT_ID
    ,NULL AS FN_PRINCIPAL_AMOUNT
    ,NULL AS FN_FINANCE_CHARGE_AMOUNT
    ,NULL AS FN_PRINCIPAL_BALANCE
    ,NULL AS FN_FINANCE_CHARGE_BALANCE
    ,NULL AS FN_ADDITIONAL_CHARGE_BALANCE
    ,NULL AS FD_ORIGINATION_DATE
    ,NULL AS FD_FIRST_PAYMENT
    ,NULL AS FD_DUE_DATE
    ,NULL AS FN_APR
    ,NULL AS FI_ADDITIONAL_STATUS
    ,NULL AS FI_CURRENT_BALANCE_SEQ
    ,NULL AS FN_INTEREST_RATE
    ,NULL AS FI_NUMBER_OF_PAYMENTS
    ,NULL AS FI_TERM_TYPE
    ,NULL AS FI_LOAN_STATUS_ID
    ,NULL AS FC_SUBPRODUCT_ID
    ,NULL AS FI_ACCRUED_TYPE_ID
    ,NULL AS FI_RULE_ID
    ,NULL AS FC_END_USER
    ,NULL AS FI_TRANSACTION
    ,NULL AS FD_OPERATION_DATE
    ,NULL AS FC_UUID_TRACKING
    ,NULL AS FC_IP_ADDRESS
    ,NULL AS FC_DEVICE
    ,NULL AS FI_LOAN_BALANCE_ID
    ,NULL AS FI_LOAN_OPERATION_ID
    ,NULL AS FI_BALANCE_SEQ
    ,NULL AS FN_PRINCIPAL_BALANCE
    ,NULL AS FN_FINANCE_CHARGE_BALANCE
    ,NULL AS FN_ADDITIONAL_CHARGE_BALANCE
    FROM DUAL WHERE CSG_1 = CSG_0;
    SC_CREDIT.SP_ERROR_LOG(CSG_SP_EXE_CUSTOMER_INFO, SQLCODE, SQLERRM,
      DBMS_UTILITY.FORMAT_ERROR_BACKTRACE, CSG_X, CSG_X);

  WHEN OTHERS THEN
  PA_STATUS_CODE := SQLCODE;
  PA_STATUS_MSG := SQLERRM || CSG_ARROW || DBMS_UTILITY.FORMAT_ERROR_BACKTRACE;
  SC_CREDIT.SP_ERROR_LOG(CSG_SP_EXE_CUSTOMER_INFO, SQLCODE, SQLERRM,
    DBMS_UTILITY.FORMAT_ERROR_BACKTRACE, CSG_X, CSG_X);

  END SP_EXE_CUSTOMER_INFO;

/

GRANT EXECUTE ON SC_CREDIT.SP_EXE_CUSTOMER_INFO TO USRNCPCREDIT1
/
