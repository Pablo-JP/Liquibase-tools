CREATE OR REPLACE  PROCEDURE SC_CREDIT.SP_BTC_SEL_DELINQUENT 
    (PA_FIRST_CENTER_ID        IN SC_CREDIT.TA_LOAN.FI_ADMIN_CENTER_ID%TYPE
    ,PA_END_CENTER_ID          IN SC_CREDIT.TA_LOAN.FI_ADMIN_CENTER_ID%TYPE
    ,PA_TODAY                  IN VARCHAR2
    ,PA_STATUS_CODE            OUT NUMBER
    ,PA_STATUS_MSG             OUT VARCHAR2
    ,PA_CUR_FEES               OUT SC_CREDIT.PA_TYPES.TYP_CURSOR)
IS
    /* **************************************************************
    * PROJECT: LOAN-LIFE-CYCLE
    * DESCRIPTION: SELECT LOAN DELINQUENT
    * CREATED DATE: 14/11/2024
    * CREATOR: ITZEL TRINIDAD RAMOS
    * MODIFICATION DATE: 05/12/2024
    ************************************************************** */
    CSL_DATE         CONSTANT VARCHAR2(10)   := 'MM/DD/YYYY';
    CSL_0            CONSTANT SIMPLE_INTEGER := 0;
    CSL_1            CONSTANT SIMPLE_INTEGER := 1;
    CSL_3            CONSTANT SIMPLE_INTEGER := 3;
    CSL_MSG_SUCCESS  CONSTANT VARCHAR2(7)   := 'SUCCESS';
    CSL_SP           CONSTANT SIMPLE_INTEGER := 2;
    CSL_ARROW        CONSTANT VARCHAR2(2)    := '->';
    CSL_COMMA        CONSTANT VARCHAR2(3) := ' , ';
BEGIN
    PA_STATUS_CODE := CSL_0;
    PA_STATUS_MSG  := CSL_MSG_SUCCESS;
    PA_CUR_FEES :=  NULL;

    OPEN PA_CUR_FEES FOR
        SELECT TL.FI_LOAN_ID AS FI_LOAN_ID
             ,TL.FI_ADMIN_CENTER_ID AS FI_ADMIN_CENTER_ID
             ,TL.FC_CUSTOMER_ID AS FC_CUSTOMER_ID
             ,TL.FI_COUNTRY_ID AS FI_COUNTRY_ID
             ,TL.FI_COMPANY_ID AS FI_COMPANY_ID
             ,TL.FI_BUSINESS_UNIT_ID AS FI_BUSINESS_UNIT_ID
             ,TL.FN_PRINCIPAL_BALANCE AS FN_PRINCIPAL_BALANCE
             ,TL.FN_ADDITIONAL_CHARGE_BALANCE AS FN_ADDITIONAL_CHARGE_BALANCE
             ,TL.FN_FINANCE_CHARGE_BALANCE AS FN_FINANCE_CHARGE_BALANCE
             ,TL.FI_PRODUCT_ID AS FI_PRODUCT_ID
             ,TL.FI_RULE_ID AS FI_RULE_ID
             ,TL.FI_CURRENT_BALANCE_SEQ AS FI_CURRENT_BALANCE_SEQ
             ,TL.FI_LOAN_STATUS_ID AS FI_LOAN_STATUS_ID
             ,TLSD.FI_COUNTER_DAY AS FI_COUNTER_DAY
             ,TLSD.FI_PAYMENT_NUMBER_ID AS FI_PAYMENT_NUMBER_ID
             ,TLSD.FI_ACTION_DETAIL_ID AS FI_ACTION_DETAIL_ID
             ,PS.FN_PAYMENT_AMOUNT AS FN_PAYMENT_AMOUNT
             ,SC_CREDIT.FN_SEL_LOAN_BALANCE_DET_JSON
                      (TL.FI_LOAN_ID
                      ,TL.FI_ADMIN_CENTER_ID
                      ,TL.FI_CURRENT_BALANCE_SEQ
                      ,NULL) AS BALANCE_DET_JSON
        FROM SC_CREDIT.TA_LOAN TL
        INNER JOIN SC_CREDIT.TA_LOAN_STATUS_DETAIL TLSD
            ON TL.FI_LOAN_ID = TLSD.FI_LOAN_ID
            AND TL.FI_ADMIN_CENTER_ID = TLSD.FI_ADMIN_CENTER_ID
            AND TL.FI_LOAN_STATUS_ID = TLSD.FI_LOAN_STATUS_ID
        INNER JOIN SC_CREDIT.TA_PAYMENT_SCHEDULE PS
            ON TL.FI_LOAN_ID = PS.FI_LOAN_ID
            AND TL.FI_ADMIN_CENTER_ID = PS.FI_ADMIN_CENTER_ID
            AND TLSD.FI_PAYMENT_NUMBER_ID = PS.FI_PAYMENT_NUMBER_ID
        WHERE TLSD.FI_LOAN_STATUS_ID = CSL_3
        AND TLSD.FI_ON_OFF = CSL_1
        AND TLSD.FI_COUNTER_DAY >= CSL_1
        AND TLSD.FI_ADMIN_CENTER_ID BETWEEN PA_FIRST_CENTER_ID AND PA_END_CENTER_ID
        AND TLSD.FD_MODIFICATION_DATE < TO_DATE(PA_TODAY, CSL_DATE)
        AND TLSD.FI_REGISTRATION_NUMBER >= CSL_1;
EXCEPTION
WHEN OTHERS THEN
    PA_STATUS_CODE := SQLCODE;
    PA_STATUS_MSG := SQLERRM || CSL_ARROW || DBMS_UTILITY.FORMAT_ERROR_BACKTRACE;

    SC_CREDIT.SP_BATCH_ERROR_LOG (UTL_CALL_STACK.SUBPROGRAM(CSL_1)(CSL_SP)
                                     ,SQLCODE
                                     ,SQLERRM
                                     ,DBMS_UTILITY.FORMAT_ERROR_BACKTRACE
                                     ,CSL_0
                                     ,PA_FIRST_CENTER_ID || CSL_COMMA || PA_END_CENTER_ID || CSL_COMMA || PA_TODAY
                                     );
END SP_BTC_SEL_DELINQUENT;

/

GRANT EXECUTE ON SC_CREDIT.SP_BTC_SEL_DELINQUENT TO USRNCPCREDIT1
/
GRANT EXECUTE ON SC_CREDIT.SP_BTC_SEL_DELINQUENT TO USRBTCCREDIT1
/
